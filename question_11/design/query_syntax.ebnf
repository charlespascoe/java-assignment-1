query = condition, [sort] ;

sort = " sort ", sortField, {", ", sortField} ;

sortField = field, (" asc" | " desc") ;

condition = "(", condition, ")"
    | text_condition
    | numeric_condition
    | "not ", condition
    | condition, " and ", condition
    | condition, " or ", condition;

text_condition = text_field, " ", text_comparator, " ", text ;
numeric_condition = numeric_field, " ", numeric_comparator, " ", number ;

field = text_field | numeric_field ;
text_field = "id" | "title" | author_field | "publisher" | "status";
author_field = "author.", ("first_name" | "second_name") ;
numeric_field = "publication_year" ;

numeric_comparator = equality_comparator | ">" | "<" | ">=" | "<=" ;
text_comparator = equality_comparator | contains_comparator ;
contains_comparator = "~~" ;
equality_comparator = "==" ;

number = digit, { digit } ;
digit = "0"-"9" ;

text = ? Non-whitespace characters ?
    | '"', ? Characters except double quotes ? , '"'
    | "'", ? Characters except single quotes ? , "'" ;
